# Maintainer: Neale Swinnerton <neale@sw1nn.com>
pkgbase=sw1nn-pkg-repo
pkgname=('sw1nn-pkg-repo' 'sw1nn-pkg-upload')
pkgver=1.2.0
pkgrel=1
arch=('x86_64')
url="https://git.sw1nn.net/r/sw1nn-pkg-repo.git/"
license=('MIT')
makedepends=('rust' 'cargo' 'git' 'zstd')
source=("$pkgbase::git+ssh://git.sw1nn.net/sw1nn/sw1nn-pkg-repo.git#tag=v$pkgver")
sha256sums=('SKIP')

build() {
  cd "$srcdir/$pkgbase"
  unset CARGO_TARGET_DIR
  export ZSTD_SYS_USE_PKG_CONFIG=1
  cargo build --release --locked
}

check() {
  cd "$srcdir/$pkgbase"
  cargo test --release --locked
}

package_sw1nn-pkg-repo() {
  pkgdesc="Self-hosted Arch Linux package repository service with REST API"
  depends=('zstd')
  backup=('etc/sw1nn-repo/config.toml')

  cd "$srcdir/$pkgbase"

  # Install binary
  install -Dm755 "target/release/sw1nn-pkg-repo" "$pkgdir/usr/bin/sw1nn-pkg-repo"

  # Install systemd service
  install -Dm644 "assets/sw1nn-pkg-repo.service" "$pkgdir/usr/lib/systemd/system/sw1nn-pkg-repo.service"

  # Install config
  install -Dm644 "assets/config.toml" "$pkgdir/etc/sw1nn-repo/config.toml"

  # Install nginx config
  install -Dm644 "assets/nginx-repo.sw1nn.net.conf" "$pkgdir/etc/nginx/sites-available/repo.sw1nn.net.conf"

  # Install documentation
  install -Dm644 "README.md" "$pkgdir/usr/share/doc/$pkgname/README.md"

  # Create data directory
  install -dm755 "$pkgdir/var/lib/sw1nn-pkg-repo/data"
}

package_sw1nn-pkg-upload() {
  pkgdesc="Upload client for sw1nn-pkg-repo package repository"
  depends=('zstd')

  cd "$srcdir/$pkgbase"

  # Install binary
  install -Dm755 "target/release/sw1nn-pkg-upload" "$pkgdir/usr/bin/sw1nn-pkg-upload"

  # Install documentation
  install -Dm644 "README.md" "$pkgdir/usr/share/doc/$pkgname/README.md"
}
